plugins {
    id 'java-library'
    id 'maven-publish'
}

group = 'io.github.deppan'
version = '1.0.0'

java {
    sourceCompatibility = '17'
    withJavadocJar()
    withSourcesJar()
}

repositories {
    mavenCentral()
}

dependencies {
    implementation("org.msgpack:jackson-dataformat-msgpack:0.9.5")
    implementation("ch.qos.logback:logback-classic:1.4.11")
    api("org.springframework.data:spring-data-redis:3.1.3")
    api("redis.clients:jedis:4.4.3")
}

static def release() {
    return "https://s01.oss.sonatype.org/content/repositories/releases"
}

static def snapshot() {
    return "https://s01.oss.sonatype.org/content/repositories/snapshots"
}

static def branch() {
    return 'git symbolic-ref --short -q HEAD'.execute().text.trim()
}

static def latestTag() {
    def tag = 'git describe --tags'.execute().text.trim()
    if (tag.startsWith('v'))
        return tag.substring(1)
    else {
        return tag
    }
}

static def publishUrl() {
    if ("main".equalsIgnoreCase(branch())) {
        return release()
    } else {
        return snapshot()
    }
}

static def versionName() {
    if ("main".equalsIgnoreCase(branch())) {
        return latestTag()
    } else {
        return latestTag() + "-SNAPSHOT"
    }
}

afterEvaluate {
    publishing {
        publications {
            maven(MavenPublication) {
                groupId = "io.github.deppan"
                artifactId = "socket.io-java-emitter"
                version = versionName()

                from components.java
            }
        }

        repositories {
            maven {
                url = publishUrl()
                credentials {
                    username = project.property('maven.username')
                    password = project.property('maven.password')
                }
            }
        }
    }
}
plugins {
    id 'java-library'
    id 'org.jreleaser' version '1.18.0'
}

java {
    sourceCompatibility = '17'
    withJavadocJar()
    withSourcesJar()
}

repositories {
    mavenCentral()
}

dependencies {
    implementation("org.msgpack:jackson-dataformat-msgpack:0.9.7")
    implementation("com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.16.1")
    implementation 'org.slf4j:slf4j-api:2.0.17'

    compileOnly("org.springframework.data:spring-data-redis:3.2.1")
    compileOnly("redis.clients:jedis:5.1.0")

    testImplementation 'org.junit.jupiter:junit-jupiter:5.12.2'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
    useJUnitPlatform()
}

static def release() {
    return "https://s01.oss.sonatype.org/content/repositories/releases"
}

static def snapshot() {
    return "https://s01.oss.sonatype.org/content/repositories/snapshots"
}

static def branch() {
    return 'git symbolic-ref --short -q HEAD'.execute().text.trim()
}

static def latestTag() {
    def tag = 'git describe --tags'.execute().text.trim()
    if (tag.startsWith('v'))
        return tag.substring(1)
    else {
        return tag
    }
}

static def publishUrl() {
    if ("main".equalsIgnoreCase(branch())) {
        return release()
    } else {
        return snapshot()
    }
}

static def versionName() {
    if ("main".equalsIgnoreCase(branch())) {
        return latestTag()
    } else {
        return latestTag() + "-SNAPSHOT"
    }
}

jreleaser {
    project {
        name = 'socket.io-java-emitter'
        version = versionName()
        author('deppan')
        description = 'A Java implementation of socket.io-emitter.'
        inceptionYear = "2023"
        license = 'MIT'
        languages {
            java {
                version = '8'
                groupId = 'io.github.deppan'
                artifactId = 'socket.io-java-emitter'
            }
        }
    }

    gitRootSearch = true

    signing {
        active = 'ALWAYS'
        armored = true
        mode = 'FILE'
        publicKey = '/Users/deppan/.jreleaser/public.pgp'
        secretKey = '/Users/deppan/.jreleaser/private.pgp'
    }

    deploy {
        maven {
            mavenCentral.create("sonatype") {
                active = 'ALWAYS'
                url = 'https://central.sonatype.com/api/v1/publisher'
                stagingRepository('build/staging-deploy')
                setAuthorization("Basic")
                applyMavenCentralRules = true
                sign = true
                checksums = true
                sourceJar = true
                javadocJar = true
                retryDelay = 60
            }
        }
    }
}

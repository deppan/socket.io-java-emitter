plugins {
    id 'java-library'
    id 'maven-publish'
    id 'org.jreleaser' version '1.22.0'
}

group = 'io.github.deppan'
version = versionName()

java {
    sourceCompatibility = '8'
    withJavadocJar()
    withSourcesJar()
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.msgpack:jackson-dataformat-msgpack:0.9.11'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.20.1'
    implementation 'org.slf4j:slf4j-api:2.0.17'

    testImplementation 'redis.clients:jedis:7.2.0'
    testImplementation 'io.github.cdimascio:dotenv-java:3.2.0'
    testImplementation 'io.lettuce:lettuce-core:7.2.1.RELEASE'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.12.2'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
    useJUnitPlatform()
}

static def branch() {
    return 'git symbolic-ref --short -q HEAD'.execute().text.trim()
}

static def latestTag() {
    def tag = 'git describe --tags'.execute().text.trim()
    if (tag.startsWith('v'))
        return tag.substring(1)
    else {
        return tag
    }
}

static def versionName() {
    if ("main".equalsIgnoreCase(branch())) {
        return latestTag()
    } else {
        return latestTag() + "-SNAPSHOT"
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'io.github.deppan'
            artifactId = 'socket.io-java-emitter'

            from components.java

            pom {
                name = 'socket.io-java-emitter'
                description = 'A Java implementation of socket.io-emitter'
                url = 'https://github.com/deppan/socket.io-java-emitter'
                inceptionYear = '2023'
                licenses {
                    license {
                        name = 'Apache-2.0'
                        url = 'https://spdx.org/licenses/Apache-2.0.html'
                    }
                }
                developers {
                    developer {
                        id = 'deppan'
                        name = 'Deppan'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/deppan/socket.io-java-emitter.git'
                    developerConnection = 'scm:git:ssh://github.com/deppan/socket.io-java-emitter.git'
                    url = 'https://github.com/deppan/socket.io-java-emitter'
                }
            }
        }
    }

    repositories {
        maven {
            url = layout.buildDirectory.dir('staging-deploy')
        }
    }
}

jreleaser {
    project {
        name = 'socket.io-java-emitter'
        version = versionName()
        authors = ['deppan']
        description = 'A Java implementation of socket.io-emitter.'
        inceptionYear = "2023"
        license = 'MIT'
        languages {
            java {
                version = '8'
                groupId = 'io.github.deppan'
                artifactId = 'socket.io-java-emitter'
            }
        }
    }

    gitRootSearch = true

    signing {
        pgp {
            active = 'ALWAYS'
            armored = true
        }
    }

    deploy {
        maven {
            mavenCentral {
                'release-deploy' {
                    active = 'ALWAYS'
                    url = 'https://central.sonatype.com/api/v1/publisher'
                    stagingRepository('build/staging-deploy')
                }
            }
            nexus2 {
                "snapshot-deploy" {
                    active = 'SNAPSHOT'
                    snapshotUrl = 'https://central.sonatype.com/repository/maven-snapshots/'
                    applyMavenCentralRules = true
                    snapshotSupported = true
                    closeRepository = true
                    releaseRepository = true
                    stagingRepository('build/staging-deploy')
                }
            }

        }
    }
}
